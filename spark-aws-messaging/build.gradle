plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'jacoco'
    id "org.sonarqube" version "4.4.1.3373"
}

group = 'com.fabiogouw'
archivesBaseName = 'spark-aws-messaging'
version = '1.1.0'

repositories {
    mavenCentral()
}

sonar {
    properties {
        property "sonar.projectKey", "fabiogouw_spark-aws-messaging"
        property "sonar.organization", "fabiogouw"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

dependencies {
    implementation group: 'org.apache.spark', name: 'spark-core_2.12', version: '3.1.2'
    compileOnly group: 'org.apache.spark', name: 'spark-sql_2.12', version: '3.1.2'
    implementation platform('com.amazonaws:aws-java-sdk-bom:1.12.13')
    implementation 'com.amazonaws:aws-java-sdk-sqs'

    testImplementation group: 'org.netbeans.external', name: 'org-apache-commons-lang3', version: 'RELEASE130'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    testImplementation 'org.testcontainers:testcontainers:1.17.6'
    testImplementation 'org.testcontainers:junit-jupiter:1.17.6'
    testImplementation 'org.testcontainers:localstack:1.17.6'
    testImplementation group: 'org.apache.spark', name: 'spark-catalyst_2.12', version: '3.1.2'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.24.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.11.0'
}

tasks.register('javadocJar', Jar) {
    classifier = 'javadoc'
    from javadoc
}

tasks.register('sourcesJar', Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

tasks.register('copyToLib', Copy) {
    into "${buildDir}/libs/deps"
    from configurations.runtimeClasspath
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn(test)
}

test {
    useJUnitPlatform()
    dependsOn(copyToLib)
    finalizedBy(jacocoTestReport)
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Spark AWS Messaging providers'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'AWS SQS sink provider'
                url 'https://github.com/fabiogouw/spark-aws-messaging'

                scm {
                    connection 'scm:git:git://github.com/fabiogouw/spark-aws-messaging.git'
                    developerConnection 'scm:git:git@github.com:fabiogouw/spark-aws-messaging.git'
                    url 'https://github.com/fabiogouw/spark-aws-messaging'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'fabiogouw'
                        name 'Fabio Gouw'
                    }
                }
            }
        }
    }
}
